plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.eaxdev'
archivesBaseName = 'jsonresume-validator'
version '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.hibernate.validator:hibernate-validator-annotation-processor:6.1.1.Final'
    compile group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.1.1.Final'
    compile group: 'org.glassfish', name: 'javax.el', version: '3.0.1-b08'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.10.1'
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId group
            artifactId archivesBaseName
            version version

            from components.java

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST

            description 'Java library to validate JsonResume by schema: https://jsonresume.org/schema/'
            name 'JsonResume Validator'
            url 'https://github.com/eaxdev/Java-JsonResume-Validator'
            organization {
                name 'com.github.eaxdev'
                url 'https://github.com/eaxdev'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/eaxdev/Java-JsonResume-Validator/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://raw.githubusercontent.com/eaxdev/Java-JsonResume-Validator/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/eaxdev/Java-JsonResume-Validator'
                connection 'scm:https://github.com/eaxdev/Java-JsonResume-Validator.git'
                developerConnection 'scm:git://github.com/eaxdev/Java-JsonResume-Validator.git'
            }
            developers {
                developer {
                    id 'eaxdev'
                    name 'RomanSA'
                    email 'eaxdev@ya.ru'
                }
            }
        }
    }
}
